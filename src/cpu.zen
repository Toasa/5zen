usingnamespace @import("const.zen");
usingnamespace @import("assembler.zen");
const std = @import("std");
const instruction = @import("instruction.zen");
const InstructionSet = instruction.InstructionSet;
const AddInst = InstructionSet.AddInst;
const AddiInst = InstructionSet.AddiInst;
const BltInst = InstructionSet.BltInst;
const JalInst = InstructionSet.JalInst;
const JalrInst = InstructionSet.JalrInst;
const Operand = instruction.Operand;

pub const CPU = struct {
    const Self = @This();
    regs: [REG_NUM]REG_TYPE,
    pc: PROG_TYPE,
    ram: [MEM_SIZE]u8,
    rom: [MEM_SIZE]u8,

    pub fn init(rom: [MEM_SIZE] u8) Self {
        var regs = [_]REG_TYPE{0} ** REG_NUM;
        regs[1] = RET_ADDR;
        return Self {
            .regs = regs,
            .pc = 0,
            .ram = [_]u8{0} ** MEM_SIZE,
            .rom = rom,
        };
    }

    pub fn fetch(self: *Self) PROG_TYPE {
        // little endian
        const inst_byte0 = @is(PROG_TYPE, self.rom[self.pc]) << 24;
        const inst_byte1 = @is(PROG_TYPE, self.rom[self.pc+1]) << 16;
        const inst_byte2 = @is(PROG_TYPE, self.rom[self.pc+2]) << 8;
        const inst_byte3 = @is(PROG_TYPE, self.rom[self.pc+3]);
        const inst_bytes = inst_byte0 | inst_byte1 | inst_byte2 | inst_byte3;
        return inst_bytes;
    }

    pub fn decode(self: *Self, inst_bytes: PROG_TYPE) InstructionSet {
        // only RV32I
        const opcode = @truncate(u7, inst_bytes);

        // each instruction is NOT determined by `opcode`.
        switch (opcode) {
            AddInst.opcode => {
                const operand = decodeRType(inst_bytes);
                return InstructionSet.createAdd(operand);
            },
            AddiInst.opcode => {
                const operand = decodeIType(inst_bytes);
                return InstructionSet.createAddi(operand);
            },
            BltInst.opcode => {
                const operand = decodeBType(inst_bytes);
                return InstructionSet.createBlt(operand);
            },
            JalInst.opcode => {
                const operand = decodeJType(inst_bytes);
                return InstructionSet.createJal(operand);
            },
            JalrInst.opcode => {
                const operand = decodeIType(inst_bytes);
                return InstructionSet.createJalr(operand);
            },
            else => @panic("unknown opcode"),
        }
    }

    fn decodeRType(inst_bytes: PROG_TYPE) Operand {
        const rs1 = @truncate(u5, inst_bytes >> 15);
        const rs2 = @truncate(u5, inst_bytes >> 20);
        const rd = @truncate(u5, inst_bytes >> 7);
        const func3 = @truncate(u3, inst_bytes >> 12);
        const func7 = @truncate(u7, inst_bytes >> 25);
        
        const rtype = Operand.RType.init(rs1, rs2, rd, func3, func7);
        return Operand{ .R = rtype };
    }

    fn decodeIType(inst_bytes: PROG_TYPE) Operand {
        const rs1 = @truncate(u5, inst_bytes >> 15);
        const rd = @truncate(u5, inst_bytes >> 7);
        const imm_u = @truncate(u12, inst_bytes >> 20);
        const func3 = @truncate(u3, inst_bytes >> 12);
        const imm = @intCast(i12, imm_u);
        
        const itype = Operand.IType.init(rs1, rd, imm, func3);
        return Operand{ .I = itype };
    }

    fn decodeBType(inst_bytes: PROG_TYPE) Operand {
        const rs1 = @truncate(u5, inst_bytes >> 15);
        const rs2 = @truncate(u5, inst_bytes >> 20);
        const func3 = @truncate(u3, inst_bytes >> 12);

        const imm1_4 = @truncate(u4, inst_bytes >> 8);
        const imm5_10 = @truncate(u6, inst_bytes >> 25);
        const imm11 = @truncate(u1, inst_bytes >> 7);
        const imm12 = @truncate(u1, inst_bytes >> 31);

        const imm1_4_ = @intCast(u12, imm1_4);
        const imm5_10_ = @intCast(u12, imm5_10);
        const imm11_ = @intCast(u12, imm11);
        const imm12_ = @intCast(u12, imm12);

        const imm_u = (imm12_ << 11) | (imm11_ << 10) | (imm5_10_ << 4) | imm1_4_;
        const imm = @bitCast(i12, imm_u);

        const btype = Operand.BType.init(rs1, rs2, imm, func3);
        return Operand{ .B = btype };
    }

    fn decodeJType(inst_bytes: PROG_TYPE) Operand {
        const rd = @truncate(u5, inst_bytes >> 7);
        const imm10_1 = @truncate(u9, inst_bytes >> 22);
        const imm11 = @truncate(u1, inst_bytes >> 20);
        const imm12_19 = @truncate(u8, inst_bytes >> 12);
        const imm20 = @truncate(u1, inst_bytes >> 31);

        const imm10_1_ = @intCast(u20, imm10_1);
        const imm11_ = @intCast(u20, imm11);
        const imm12_19_ = @intCast(u20, imm12_19);
        const imm20_ = @intCast(u20, imm20);

        const imm_u = (imm20_ << 19) | (imm12_19_ << 11) | (imm11_ << 10) | imm10_1_;

        const imm = @bitCast(i20, imm_u);
        const jtype = Operand.JType.init(rd, imm);
        return Operand{ .J = jtype };
    }

    pub fn execute(self: *Self, inst: InstructionSet) void {
        switch(inst) {
            .Add => |add| {
                const rs1 = add.operand.R.rs1;
                const rs2 = add.operand.R.rs2;
                const rd = add.operand.R.rd;
                const data = self.regRead(rs1) + self.regRead(rs2);
                self.regWrite(rd, data);
                self.pc += 4;
            },
            .Addi => |addi| {
                const rd = addi.operand.I.rd;
                const rs1 = addi.operand.I.rs1;
                const imm = addi.operand.I.imm;
                const data = self.regRead(rs1) + imm;
                self.regWrite(rd, data);
                self.pc += 4;
            },
            .Blt => |blt| {
                const rs1 = blt.operand.B.rs1;
                const rs2 = blt.operand.B.rs2;
                const imm = blt.operand.B.imm;

                const rs1_data = self.regRead(rs1);
                const rs2_data = self.regRead(rs2);
                if (rs1_data < rs2_data) {
                    var pc_i = @bitCast(i32, self.pc);
                    pc_i += imm;
                    self.pc = @bitCast(REG_TYPE, pc_i);
                } else {
                    self.pc += 4;
                }
            },
            .Jal => |jal| {
                const rd = jal.operand.J.rd;
                const imm = jal.operand.J.imm;
                var pc_i = @bitCast(i32, self.pc);
                self.regWrite(rd, pc_i + 4);
                pc_i += imm;
                self.pc = @bitCast(PROG_TYPE, pc_i);
            },
            .Jalr => |jalr| {
                const rd = jalr.operand.I.rd;
                var pc_i = @bitCast(i32, self.pc);
                self.regWrite(rd, pc_i + 4);
                const rs1 = jalr.operand.I.rs1;
                self.pc = @bitCast(PROG_TYPE, self.regs[rs1]);
            }
        }
    }

    fn regRead(self: Self, reg: u5) REG_TYPE {
        // zero register's value is always zero.
        if (reg == 0) {
            return 0;
        }
        return self.regs[reg];
    }

    fn regWrite(self: *Self, reg: u5, data: REG_TYPE) void {
        // zero register is always zero.
        if (reg == 0) {
            return;
        }
        self.regs[reg] = data;
    }

    // for debug
    pub fn printRegs(self: Self) void {
        var i: @TagType(Reg) = 0;
        for (self.regs) |reg_val| {
            const reg = @intToEnum(Reg, i);
            const reg_name = @tagName(reg);
            std.debug.warn("{}: {}\n", reg_name, reg_val);
            i +%= 1;
        }
    }
};

// Register
pub const Reg = enum {
    Zero, // constant zero
    Ra,   // return address
    Sp,   // stack pointer
    Gp,   // global pointer
    Tp,   // thread pointer
    T0,   // tmp0
    T1,   // tmp1
    T2,   // tmp2
    S0,   // save0
    S1,   // save1
    A0,   // function argument or frame pointer
    A1,   // function argument
    A2,   // function argument
    A3,   // function argument
    A4,   // function argument
    A5,   // function argument
    A6,   // function argument
    A7,   // function argument
    S2,   // save2
    S3,   // save3
    S4,   // save4
    S5,   // save5
    S6,   // save6
    S7,   // save7
    S8,   // save8
    S9,   // save9
    S10,  // save10
    S11,  // save11
    T3,   // tmp3
    T4,   // tmp4
    T5,   // tmp5
    T6,   // tmp6
};

// Test that `reg` register value equal to `expected`.
fn testHelper(program: []PROG_TYPE, reg: Reg, expected: REG_TYPE) void {
    const Emulator = @import("emulator.zen").Emulator;
    const rom = Assembler.assemble(program);
    var emu = Emulator.init(rom);
    emu.run();

    const reg_int = @enumToInt(reg);
    std.testing.ok(emu.cpu.regRead(reg_int) == expected);
}

test "single addi instruction" {    
    const program = &[_]PROG_TYPE{
        asmAddi(Reg.T0, Reg.T0, 8),
        asmRet(),
    };
    testHelper(program, Reg.T0, 8);
}

test "multi addi instruction" {
    const program = &[_]PROG_TYPE{
        asmAddi(Reg.T1, Reg.T1, 1),
        asmAddi(Reg.T2, Reg.T1, 1023),
        asmRet(),
    };
    testHelper(program, Reg.T2, 1024);
}

test "single addi instruction with negative value" {
    const program = &[_]PROG_TYPE {
        asmAddi(Reg.T0, Reg.T0, -22),
        asmRet(),
    };
    testHelper(program, Reg.T0, -22);
}

test "add instruction" {
    const program = &[_]PROG_TYPE{
        asmAddi(Reg.T0, Reg.T0, 1),
        asmAddi(Reg.T1, Reg.T1, 1023),
        asmAdd(Reg.T2, Reg.T1, Reg.T0),
        asmRet(),
    };
    testHelper(program, Reg.T2, 1024);
}

test "jal instruction" {
    const program = &[_]PROG_TYPE{
        asmAddi(Reg.T1, Reg.T1, 10),
        asmJal(Reg.Zero, 8),
        // jump this instruction
        asmAddi(Reg.T1, Reg.T1, 20),
        asmRet(),
    };
    testHelper(program, Reg.T1, 10);
}

test "blt instruction" {
    const program = &[_]PROG_TYPE{
        asmAddi(Reg.T1, Reg.T1, 11),
        // while (t2 < t1) {
        //     t2 += 2;
        // }
        asmAddi(Reg.T2, Reg.T2, 2),
        asmBlt(Reg.T2, Reg.T1, -4),
        asmRet(),
    };
    testHelper(program, Reg.T2, 12);
}